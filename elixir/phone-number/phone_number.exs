defmodule Phone do

  import String, only: [ slice: 2, replace: 3, pad_leading: 2 ]

  @doc """
  Remove formatting from a phone number.

  Returns "0000000000" if phone number is not valid
  (10 digits or "1" followed by 10 digits)

  ## Examples

  iex> Phone.number("212-555-0100")
  "2125550100"

  iex> Phone.number("+1 (212) 555-0100")
  "2125550100"

  iex> Phone.number("+1 (212) 055-0100")
  "0000000000"

  iex> Phone.number("(212) 555-0100")
  "2125550100"

  iex> Phone.number("867.5309")
  "0000000000"
  """
  @spec number(String.t) :: String.t
  def number(raw) do
    phone = raw |> replace(~r/[^\d\w]/, "")
                |> pad_leading(11)
                |> slice(-11..-1)
    if valid?(phone), do: slice(phone, 1..-1), else: "0000000000"
  end

  defp valid?(phone) do
    phone =~ ~r/^
                (1|\ )     # country code (or empty)
                [2-9]\d{2} # area code
                [2-9]\d{6} # exchange code & subscriber number
               /x
  end

  @doc """
  Extract the area code from a phone number

  Returns the first three digits from a phone number,
  ignoring long distance indicator

  ## Examples

  iex> Phone.area_code("212-555-0100")
  "212"

  iex> Phone.area_code("+1 (212) 555-0100")
  "212"

  iex> Phone.area_code("+1 (012) 555-0100")
  "000"

  iex> Phone.area_code("867.5309")
  "000"
  """
  @spec area_code(String.t) :: String.t
  def area_code(raw), do: number(raw) |> slice(0..2)

  @doc """
  Pretty print a phone number

  Wraps the area code in parentheses and separates
  exchange and subscriber number with a dash.

  ## Examples

  iex> Phone.pretty("212-555-0100")
  "(212) 555-0100"

  iex> Phone.pretty("212-155-0100")
  "(000) 000-0000"

  iex> Phone.pretty("+1 (303) 555-1212")
  "(303) 555-1212"

  iex> Phone.pretty("867.5309")
  "(000) 000-0000"
  """
  @spec pretty(String.t) :: String.t
  def pretty(raw) do
    <<area::binary-3, exchange::binary-3, subscriber::binary>> = number(raw)
    "(#{area}) #{exchange}-#{subscriber}"
  end

end
